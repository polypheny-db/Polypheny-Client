
group "ch.unibas.dmi.dbis.polyphenydb-client"


def versionMajor = 1
def versionMinor = 0
def versionQualifier = "SNAPSHOT"
version = versionMajor + "." + versionMinor + "-" + versionQualifier


buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
		// Shadowing; used to make a fat jar (https://github.com/johnrengelman/shadow)
        classpath group: "com.github.jengelman.gradle.plugins", name: "shadow", version: "5.0.0"
        // Google Protocol Buffers (https://github.com/google/protobuf-gradle-plugin)
        classpath group: "com.google.protobuf", name: "protobuf-gradle-plugin", version: "0.8.7"
        // Lombok (https://plugins.gradle.org/plugin/io.freefair.lombok)
        //classpath group: "io.freefair.gradle", name: "lombok-plugin", version: "3.6.6"
    }
}


repositories {
    maven {
        // DBIS Artifactory
        url "https://dbis-artifactory.dmi.unibas.ch/maven2/"
    }
    maven {
        url "https://clojars.org/repo/"
    }
    flatDir {
        dirs "libs"
    }
    mavenCentral()
    jcenter()
}


apply plugin: "java"
apply plugin: "maven"
apply plugin: "idea"
apply plugin: "com.google.protobuf"
//apply plugin: "io.freefair.lombok"
apply plugin: "com.github.johnrengelman.shadow"


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

def protobufVersion = "3.6.1"
def grpcVersion = "1.21.0"



dependencies {
    compile name: "PolySqlParser-1.0"
    //compile name: "voltdbclient-6.6.4"

    ////// CHRONOS
    compile group: "ch.unibas.dmi.dbis.chronos", name: "chronos-agent", version: "2.2.0"

    ////// JDBC
    //runtime files("libs/Polypheny-DB-JDBC-Driver-1.0-SNAPSHOT.jar")
    //runtime files("libs/postgresql-42.0.0.jar")
    //compile group: "ch.unibas.dmi.dbis.polyphenydb", name: "polypheny-db-jdbc-driver", version: "0.2.0-SNAPSHOT"
    compile (group: 'ch.unibas.dmi.dbis.polyphenydb', name: 'polyphenydb-jdbc-driver', version: '1.0-SNAPSHOT'){exclude group: "com.google.protobuf"}

    compile group: "org.mariadb.jdbc", name: "mariadb-java-client", version: "1.8.0"
    compile group: "monetdb", name: "monetdb-jdbc", version: "2.28"
    compile group: "mysql", name: "mysql-connector-java", version: "6.0.6"

    ////// PROTO
    compile group: "com.google.protobuf", name: "protobuf-java", version: protobufVersion
    compile group: "com.google.protobuf", name: "protobuf-java-util", version: protobufVersion
    compile group: "io.grpc", name: "grpc-all", version: grpcVersion

    ////// COMMON
    compile group: "commons-io", name: "commons-io", version: "2.6"
    compile group: "org.apache.commons", name: "commons-math3", version: "3.6.1"
    compile group: "com.google.guava", name: "guava", version: "27.1-jre"

    ///// TPCC
    compile group: "com.vdurmont", name: "emoji-java", version: "4.0.0"

    /////// YCSB Top-Level
    compile group: "com.google.collections", name: "google-collections", version: "1.0"
    compile group: "org.jdom", name: "jdom", version: "1.1"

    /////// YCSB CORE
    compile group: "org.apache.htrace", name: "htrace-core4", version: "4.1.0-incubating"
    compile group: "org.codehaus.jackson", name: "jackson-mapper-asl", version: "1.9.4"
    compile group: "org.codehaus.jackson", name: "jackson-core-asl", version: "1.9.4"
    compile group: "org.hdrhistogram", name: "HdrHistogram", version: "2.1.4"

    /////// YCSB JDBC
    compile group: "org.apache.openjpa", name: "openjpa-jdbc", version: "2.2.1"
    compile group: "org.hsqldb", name: "hsqldb", version: "2.3.3"

    ////// LOGGING
    compile group: "org.slf4j", name: "slf4j-api", version: "1.7.26"
    compile group: "org.apache.logging.log4j", name: "log4j-slf4j-impl", version: "2.11.2"
    compile group: "org.apache.logging.log4j", name: "log4j-core", version: "2.11.2"

    ////// CLI PARSING
    compile group: "com.github.rvesse", name: "airline", version: "2.7.0"

    ////// NETDATA
    // Rest
    compile group: "com.squareup.okhttp3", name: "okhttp", version: "3.14.2"
    compile group: "com.mashape.unirest", name: "unirest-java", version: "1.4.9"
    compile group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.9"

    ////// JSON
    compile group: "com.google.code.gson", name: "gson", version: "2.8.5"

    // https://mvnrepository.com/artifact/com.mchange/c3p0
    compile group: "com.mchange", name: "c3p0", version: "0.9.5.4"

    // Java EE modules have been removed in Java 11
    compile group: "javax.xml.bind", name: "jaxb-api", version: "2.3.1"

    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compile group: "javax.annotation", name: "javax.annotation-api", version: "1.3.2"
    }


    // --- Test Compile ---
    testCompile group: "junit", name: "junit", version: "4.12"
    testCompile group: "org.testng", name: "testng", version: "6.10"
}


def generatedDirName = "${project.buildDir}/generated"


sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "${protobuf.generatedFilesBaseDir}/main/java", "${protobuf.generatedFilesBaseDir}/main/grpc"]

            outputDir = file(project.buildDir.absolutePath + "/classes")
        }
        resources {
            srcDirs = ["src/main/resources","src/main/proto"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/classes")
    }
    test {
        java {
            srcDirs = ["src/test/java"]
            outputDir = file(project.buildDir.absolutePath + "/test-classes")
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/test-classes")
    }
}


//delombok.enabled = false


/**
 * Protobuf
 */
protobuf {
    // Configure the protoc executable
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }

    generatedFilesBaseDir = "${generatedDirName}/proto"
}



/**
 * JavaDoc
 */
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
    // suppress most of the warnings
    options.addStringOption("Xdoclint:none", "-quiet")
    // Include private fields in JavaDoc
    options.memberLevel = JavadocMemberLevel.PRIVATE
	
	options.tags = ["tpccversion"]
    exclude "**/com/yahoo/ycsb**"
    options.setOutputLevel(JavadocOutputLevel.QUIET)
    options.quiet()
}



/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "Databases and Information Systems Group, Department Mathematics and Computer Science, University of Basel, Switzerland"
        attributes "Version": "$version"
    }
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}
shadowJar {
    classifier = ''
}
assemble.dependsOn shadowJar
artifacts {
    //archives jar // regular jar containing only the compiled source
    archives shadowJar // fat jar which additionally contains all dependencies
    archives sourcesJar // jar file containing the java doc files
    archives javadocJar // jar file containing the source files
}




/**
 * IntelliJ
 */
idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        inheritOutputDirs = false
        outputDir = file("${project.buildDir}/classes")
        testOutputDir = file("${project.buildDir}/test-classes")
		
		 // If you have additional sourceSets and/or codegen plugins, add all of them
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")

        excludeDirs += file("input")
        excludeDirs += file("output")
        excludeDirs += file("storage")
    }
}